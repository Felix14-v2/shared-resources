plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}

loom {
    // im not sure what this does,
    // but i think we might need it
    runConfigs.configureEach {
        ideConfigGenerated = true
    }
}

version = project.mod_version
group = project.maven_group

// stuff that applies to all projects
allprojects {
    apply plugin: "maven-publish"
    apply plugin: "fabric-loom"

    // ideally this would be java 8 for mc16
    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    java {
        withSourcesJar()
    }

    // add repos for all projects
    repositories {
        maven {
            name "Terraformers Maven"
            url 'https://maven.terraformersmc.com'
        }
        maven {
            name "Shedaniel Maven"
            url "https://maven.shedaniel.me/"
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content { includeGroup "maven.modrinth" }
        }
    }

    // floader needs to be in every sub module
    dependencies {
        modApi "net.fabricmc:fabric-loader:${loader_version}"
    }

    processResources {
        inputs.property "version", version

        filesMatching("fabric.mod.json") {
            expand "version": version
        }

    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}

dependencies {
    // dummy versions
    minecraft "com.mojang:minecraft:1.19.2"
    mappings "net.fabricmc:yarn:1.19.2+build.8:v2"

    modImplementation "com.terraformersmc:modmenu:4.0.6"
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // JiJ every sub project
    subprojects.each {
        include project("${it.name}")
    }
}

// stuff that applies to all sub projects
subprojects {
    dependencies {
        // include the root project because that's where our common code is
        implementation (rootProject) {
            // having 2 floaders from the sub project and root project causes issues so we get rid of one.
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
}